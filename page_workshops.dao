load web.html import html
load config import configuration
load translations import translation
load lib import lib
load html_common import html_content

namespace html_content { page_workshops }

routine page_workshops(
    invar page_name: string,
    path: string,
    invar trans: Locale,
    invar dst: string,
    invar pages: map<string, string>) => none {
  html5 = io.open(path, 'w')
  defer { html5.close() }

  # just convenient shortcuts
  invar cu = conf.uri
  invar ci = conf.img
  invar t = translations[(Locale)trans]

  rel = routine(invar target: string) { return mkrel(dst, target) }

  # FIXME it should be somehow allowed
  _html_head = html_head(trans, dst, pages)
  _html_top_stripe = html_top_stripe(page_name, trans, dst, pages)
  _html_footer = html_footer(trans)

  html5.write(document {
    text(_html_head)

    body {
      text(_html_top_stripe)

      div(id='workshops') {
        h3(_class='section_red') { t['workshops'] }
        participants.iterate { [p]
          p.events.iterate { [e]
            if (e.kind == $workshop) {
              div(_class='workshop') {
                paragraph(_class='event_title') { e.trans[(Locale)trans].name }
                paragraph(_class='participant_name') { p.name }
                paragraph(_class='event_desc') { e.trans[(Locale)trans].description }
                paragraph(_class='participation_reqs') { t['participation_reqs'] }
                paragraph(_class='event_reqs') { e.trans[(Locale)trans].requirements }
              }
            }
          }
        }
      }

      text(_html_footer)
    }
  })
}
