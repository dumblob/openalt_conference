#FIXME partners - block
#FIXME add stripes over participants' photos
#FIXME add year under the the header under the black stripe
#      probably before the stripe and then through CSS away from there
#FIXME remove unnecessary ids and classes
#FIXME rewrite the table generation in schedule using paragraphs in cells

load web.html import html
# conflicting html tag name with namespace name
load time as my_time
load config import configuration
load translations import translation
load lib import lib
load html_common import html_content

namespace html_content { page_home }

routine page_home(
    invar path: string,
    invar trans: string,
    invar dst: string,
    invar pages: map<string, string>) => none {
  html5 = io.open(path, 'w')
  defer { html5.close() }

  # just convenient shortcuts
  invar cu = conf.uri
  invar ci = conf.img
  invar t = translations[trans]

  rel = routine(invar target: string) { return mkrel(dst, target) }

  _html_head = html_head(trans, dst)
  # FIXME it should be somehow allowed
  _top_stripe = top_stripe('home', trans, dst, pages)

  html5.write(document {
    text(_html_head)
    body {
      text(_top_stripe)
      div(id='header') {
        img(id='logo_big', src=rel(ci.logo_big), alt=t['logo_big'])
        paragraph(_class='narrow_paragraph') { t['motivation'] }
        paragraph(id='time_range') {
          strong {
            localized_time_format(
              t['_locale'], conf.time_from, '%-d.') + '-' +
            localized_time_format(
              t['_locale'], conf.time_to, '%-d. %B %Y')
          }
        }
      }
      div(id='main_buttons') {
        anchor(href='#talks')     { t['talks'] }
        anchor(href='#workshops') { t['workshops'] }
        anchor(href='#schedule')  { t['schedule'] }
      }
      div(id='speakers') {
        h3(_class='section') { t['speakers'] }
        div {
          paragraph {
            for (pa in participants) {
              if (pa.events.find { X.kind == $internal } == none) {
                img(_class='speaker', src=rel(pa.photo), alt=pa.name)
              }
            }
          }
        }
        div(_class='clear_float') {}
      }
      div(id='talks') {
        h3(_class='section_red') { t['talks'] }
        participants.iterate { [p]
          p.events.iterate { [e]
            #img(_class='talk', src=rel(tk[1]), alt=tk[0])
            if (e.kind == $talk) {
              div(_class='talks') {
                paragraph(_class='event_title') { e.trans[trans].name }
                paragraph(_class='participant_name') { p.name }
                paragraph(_class='event_desc') { e.trans[trans].description }
              }
            }
          }
        }
      }
      div(id='workshops') {
        h3(_class='section_red') { t['workshops'] }
        participants.iterate { [p]
          p.events.iterate { [e]
            if (e.kind == $workshop) {
              div(_class='workshop') {
                paragraph(_class='event_title') { e.trans[trans].name }
                paragraph(_class='participant_name') { p.name }
                paragraph(_class='event_desc') { e.trans[trans].description }
                paragraph(_class='participation_reqs') { t['participation_reqs'] }
                paragraph(_class='event_reqs') { e.trans[trans].requirements }
              }
            }
          }
        }
      }
      div(id='schedule') {
        h3(_class='section') { t['schedule'] }

        # FIXME reuse the sorted items (use guard?)
        events = {}
        participants.iterate { [p]
          p.events.iterate { events.append((X, p)) }
        }
        # according to time and place
        events = events.sort {
          # FIXME why need to use value?
          if (X[0].from.value == Y[0].from.value)
            return X[0].trans[trans].place < Y[0].trans[trans].place
          else
            return X[0].from < Y[0].from
        }

        for (i = 0; i < %events;) {
          last_from = events[i][0].from

          paragraph {
            localized_time_format(t['_locale'], last_from, '%A %-d. %-m. %Y')
          }

          table {
            for ((border_top, first_iter) = (true, true);
                 i < %events; i += 1) {
              (e, participant) = events[i]

              if (e.from.year  == last_from.year &&
                  e.from.month == last_from.month &&
                  e.from.day   == last_from.day) {
                # FIXME why need to use value?
                if (e.from.value == last_from.value && ! first_iter)
                  border_top = false
                else
                  border_top = true
              }
              else {
                break
              }

              tr {
                # FIXME class doesn't work - why?
                td(_class=(border_top) ? 'with_border_top' : '') {
                  # FIXME don't mention this image in HTML,
                  #   but use it only in CSS as decoration?
                  img(src=rel(ci.clock))
                  text(e.from.format('%-H:%M'))
                  if (%e.trans[trans].place) {
                    img(src=rel(ci.door))
                    text(e.trans[trans].place)
                  }
                }
                td(_class=(border_top) ? 'with_border_top' : '') {
                  strong { e.trans[trans].name }
                  if (e.kind != $internal) {
                    switch (e.kind) {
                      case $workshop: text(' / ' + t['workshop'])
                      case $talk:     text(' / ' + t['talk'])
                    }
                    text(' / ' + participant.name + ' (' +
                      (string)(my_time.time.diff(e.from, e.to).seconds/60) +
                      ' min)')
                  }
                }
              }

              last_from = e.from
              first_iter = false
            }
          }
        }
      }
      div(id='location') {
        h3(_class='section') { t['location'] }
        paragraph { t['address'] }
        img(src=rel(ci.location), alt=t['location'] + ': ' + conf.coord_gps)
      }
      div(id='partners') {
        h3(_class='section') { t['partners'] }
        paragraph {
          for (invar p in partners) {
            anchor(href=p.uri) {
              img(_class='partner', src=rel(p.img), alt=p.name)
            }
          }
        }
      }
      div(id='footer') {
        paragraph { t['footer'] }
      }
    }
  })
}
