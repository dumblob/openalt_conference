#!/usr/bin/env dao

# FIXME check all colors to match the design
# FIXME add links/names to proper parts (workshops...)
# FIXME obrazky partneru ze stareho openalt.cz v co nejvyssim rozliseni
# FIXME vypisy WARN

load web.html import html
# conflicting html tag name with namespace name
load time as my_time
load os.fs
load sys
load 'config.dao' import configuration
load 'translations.dao' import translation

# /some/full/path/to/file/or/dir/aka/target
# /some/full/path/with/my/current/directory/I/am/in/aka/from/
# ->
# ../../../../../../../../../to/file/or/dir/aka/target
routine mkrel(from: string, invar target: string) => string {
  # FIXME check that paths are normalized (can't use fs.realpath(), because
  #   it requires file existence) and full and not empty
  if (from  [0] != '/'[0]) std.error('"from" is not a full path')
  if (from [-1] != '/'[0]) from += '/'
  if (target[0] != '/'[0]) {
    if (target.match('^[^:/]+') == none)
      std.error('"target" is not a full path nor URI')
    else
      return target
  }

  shared = 1  # /
  while (1) {
    x = target[shared:].fetch('[^/]+')
    y = from[shared:].fetch('[^/]+')
    if (%x && x == y)
      shared += %x +1  # +1 for /
    else
      break
  }
  return from[shared:].change('[^/]+', '..') + target[shared:]
}

routine gen_pages(invar trans: string) {
  # just convenient shortcuts
  invar cu = conf.uri
  invar ci = conf.img
  invar t = translations[trans]

  d = fs.dir(conf.rootdir)
  if (! d.exists(trans)) d.mkdir(trans)
  invar dst = conf.rootdir + '/' + trans

  rel = routine(invar target: string) { return mkrel(dst, target) }

  main_html = 'main.html'
  html5 = io.open(dst + '/' + main_html, 'w')
  defer { html5.close() }

  html5.write(document {
    head {
      title { t['title'] }
      # convenient for relative URLs (see http://www.ietf.org/rfc/rfc1808.txt)
      base(href=conf.base_uri)
      # FIXME why charset must be separately from http_eqiv?
      meta(charset='utf-8')
      meta(http_equiv=$content_type, content='text/html')
      meta(name='viewport', content='width=device-width')
      meta(name='Keywords', content='k1,k2,k3')
      meta(name='Description', content=conf.meta_description)
      link(rel=$icon, _type=conf.favicon.mime, href=rel(conf.favicon.path),
           sizes=conf.favicon.sizes)
      link(rel=$stylesheet, _type='text/css', href=rel(cu.css))
    }
    body {
      # FIXME span input anchor text paragraph italic table h1
      #   (style=''
      div(id='top_stripe') {
        div(id='top_stripe_sub00') {
          div(id='top_stripe_nav') {
            anchor(href='#')                     { t['home'] }
            anchor(href=rel(dst + '/about'    )) { t['about'] }
            anchor(href=rel(dst + '/thanks'   )) { t['thanks'] }
            anchor(href=rel(dst + '/gallery'  )) { t['gallery'] }
            anchor(href=rel(dst + '/downloads')) { t['downloads'] }
            anchor(href=rel(dst + '/contact'  )) { t['contact'] }
          }
          div(id='top_stripe_right_part') {
            div(id='top_stripe_links') {
              anchor(href=rel(cu.rss)) {
                img(_class='TopStripeLinks', src=rel(ci.rss),
                    alt=t['rss'])
              }
              anchor(href=rel(cu.twitter)) {
                img(_class='TopStripeLinks', src=rel(ci.twitter),
                    alt=t['twitter'])
              }
              anchor(href=rel(cu.fb)) {
                img(_class='TopStripeLinks', src=rel(ci.fb),
                    alt=t['fb'])
              }
            }
            div(id='top_stripe_lang') {
              i = 0
              for (invar lang in translations) {
                if (i)
                  text(' / ')
                else
                  i += 1
                if (lang[0] == trans)
                  strong { lang[0] }
                else
                  anchor(href='../' + lang[0] + '/' + main_html) { lang[0] }
              }
            }
          }
        }
      }
      div(id='header') {
        paragraph {
          img(id='logo_big', src=rel(ci.logo_big), alt=t['logo_big'])
        }
        paragraph { t['motivation'] }
        paragraph(id='time_range') {
          strong {
            # FIXME make it LOCALE-aware and then translation tr-aware
            conf.time_from.format('%-d.') + '-' +
            conf.time_to.format('%-d. %B %Y')
          }
        }
      }
      div(id='main_buttons') {
        anchor(id='main_button0', href='#talks')     { t['talks'] }
        anchor(id='main_button1', href='#workshops') { t['workshops'] }
        anchor(id='main_button2', href='#schedule')  { t['schedule'] }
      }
      div(id='speakers') {
        h3(_class='section') { t['speakers'] }
        paragraph {
          for (pa in participants) {
            if (pa.events.find { X.kind == $internal } == none) {
              img(_class='speaker', src=rel(pa.photo), alt=pa.name)
            }
          }
        }
      }
      div(id='talks') {
        h3(_class='section') { t['talks'] }
        participants.iterate { [p]
          p.events.iterate { [e]
            #img(_class='talk', src=rel(tk[1]), alt=tk[0])
            if (e.kind == $talk) {
              paragraph(_class='event_title') { e.trans[trans].name }
              paragraph(_class='participant_name') { p.name }
              paragraph(_class='event_desc') { e.trans[trans].description }
            }
          }
        }
      }
      div(id='workshops') {
        h3(_class='section') { t['workshops'] }
        participants.iterate { [p]
          p.events.iterate { [e]
            if (e.kind == $workshop) {
              paragraph(_class='event_title') { e.trans[trans].name }
              paragraph(_class='participant_name') { p.name }
              paragraph(_class='event_desc') { e.trans[trans].description }
              paragraph(_class='participation_reqs') { t['participation_reqs'] }
              paragraph(_class='event_reqs') { e.trans[trans].requirements }
            }
          }
        }
      }
      div(id='schedule') {
        h3(_class='section') { t['schedule'] }

        # FIXME reuse the sorted items (use guard?)
        events = {}
        participants.iterate { [p]
          p.events.iterate { events.append((X, p)) }
        }
        # according to time and place
        events = events.sort {
          # FIXME why need to use value?
          if (X[0].from.value == Y[0].from.value)
            return X[0].trans[trans].place < Y[0].trans[trans].place
          else
            return X[0].from < Y[0].from
        }

        for (i = 0; i < %events;) {
          last_from = events[i][0].from

          paragraph {
            # FIXME make it thread-safe!
            #old = sys.setlocale()
            #defer { sys.setlocale($all, old) }
            #sys.setlocale($time, t['_locale'])
            last_from.format('%A %-d. %-m. %Y')
          }

          table {
            for ((border_top, first_iter) = (true, true);
                 i < %events; i += 1) {
              (e, participant) = events[i]

              if (e.from.year  == last_from.year &&
                  e.from.month == last_from.month &&
                  e.from.day   == last_from.day) {
                # FIXME why need to use value?
                if (e.from.value == last_from.value && ! first_iter)
                  border_top = false
                else
                  border_top = true
              }
              else {
                break
              }

              tr {
                # FIXME class doesn't work - why?
                td(_class=(border_top) ? 'with_border_top' : '') {
                  # FIXME don't mention this image in HTML,
                  #   but use it only in CSS as decoration?
                  img(src=rel(ci.clock))
                  text(e.from.format('%-H:%M'))
                  if (%e.trans[trans].place) {
                    img(src=rel(ci.door))
                    text(e.trans[trans].place)
                  }
                }
                td(_class=(border_top) ? 'with_border_top' : '') {
                  strong { e.trans[trans].name }
                  if (e.kind != $internal) {
                    switch (e.kind) {
                      case $workshop: text(' / ' + t['workshop'])
                      case $talk:     text(' / ' + t['talk'])
                    }
                    text(' / ' + participant.name + ' (' +
                      (string)(my_time.time.diff(e.from, e.to).seconds/60) +
                      ' min)')
                  }
                }
              }

              last_from = e.from
              first_iter = false
            }
          }
        }
      }
      div(id='location') {
        h3(_class='section') { t['location'] }
        img(src=rel(ci.location), alt=t['location'] + ': ' + conf.coord_gps)
      }
      div(id='partners') {
        h3(_class='section') { t['partners'] }
        paragraph {
          for (invar p in partners) {
            anchor(href=p.uri) {
              img(_class='partner', src=rel(p.img), alt=p.name)
            }
          }
        }
      }
      div(id='footer') {
        paragraph { t['footer'] }
      }
    }
  })
}

# USAGE: generate.dao [<translation>]...
#   if no translations given, generate all
routine main(invar ...: string as args) {
  # FIXME bug?
  #defer (any) { return 0 }

  invar chosen_trs = std.exec {
    if (%args)
      return ((list<string>)args).associate(1) { (X, translations[X]) }
    else
      return translations
  }

  for (invar trans in chosen_trs) {
    fut = gen_pages(trans[0])!!
    defer { fut.value() }
  }

  css = io.open(conf.uri.css, 'w')
  defer { css.close() }

  # FIXME definitions for the CSS below
  invar red = 'red'
  invar font = ''
  # FIXME @media only screen and (max-width: 640px) {...}
  css.write(
@[]html {
  background-color: white;
  color: black;
  font: "";
}
body {
  margin: 0;
}
#top_stripe,
#top_stripe a:link,
#top_stripe a:visited,
#top_stripe a:hover,
#top_stripe a:active {
  background-color: black;
  color: white;
  text-decoration: none;
  /*margin: 0 auto 0 auto;*/
  /*width: 700pt;*/
}
#top_stripe_sub00 {
  /*margin: auto 0 auto 100pt;*/
  /*width: 900px;*/
  margin: 0 auto 0 auto;
  position: relative;
}
#top_stripe_nav,
#top_stripe_right_part {
  display: inline-block;
}
#top_stripe_links,
#top_stripe_lang {
  display: inline;
}
#top_stripe_nav {
  width: 75%;
  margin: 10pt;
}
#top_stripe_nav > a {
  margin: 6pt;
  text-transform: uppercase;
  font-size: 10pt;
}
#top_stripe_right_part {
  display: inline;
  width: 25%;
  /*margin: 0 0 0 auto;*/
}

#header {
  margin: 0;
  width: 100%;
  padding: 0 auto;
}
#time_range {
  font-size: 15pt;
}
#main_buttons {
  margin: 0 auto;
}
#main_buttons a,
#main_buttons a:link,
#main_buttons a:visited,
#main_buttons a:hover,
#main_buttons a:active {
  color: white;
  padding: 20pt 50pt;
  /*height: 135pt;
  width: 65pt;*/
  margin: 0 10pt 0 10pt;
}
#main_button0 {
  background-color: red;
}
#main_button1 {
  background-color: green;
}
#main_button2 {
  background-color: blue;
}
#logo_big {
  /*position: fixed;*/
  /*width: 512px;
  margin: 0 auto 0 auto;*/
}
#speakers,
#talks,
#workshops,
#schedule,
#location,
#partners {
  padding-bottom: 38pt;
}
.section {
  font-size: 30pt;
  font-weight: 200;
  text-transform: uppercase;
  padding: 30pt 0 0 0;
  margin: 0;
}
#talks > h3, #workshops > h3 {
  color: red;
}
.event_title {
  color: black;
  font-weight: 900;
  font-size: 13pt;
  text-transform: uppercase;
  margin: 30pt auto 0 auto;
}
.participant_name {
  color: red;
  font-size: 13pt;
  font-weight: 500;
  margin: 3pt auto;
}
.event_desc {
  margin: 8pt auto 0 auto;
}
.participation_reqs {
  font-weight: bold;
  margin: 3pt auto 0 auto;
}
.participation_reqs, .event_reqs {
  font-size: 10pt;
}
.event_reqs {
  margin: 2pt auto 0 auto;
}
#speakers, #location {
  background-color: #404040;
  color: white;
}
.speaker {
  height: 150px;
  width: 150px;
  margin: 30px;
  padding: 0;
}
#workshops {
  background-color: #c0c0c0;
}
#schedule {
  background-color: #e03030;
  color: white;
}
#schedule table {
  border-collapse: collapse
}
#schedule > table tr:last-child > td {
  border-bottom: 2px solid white;
}
.with_border_top {
  border-top: 2px solid white;
  padding-top: 17pt;
}
#schedule td {
  border-right: 2px solid white;
  border-left: 2px solid white;
  padding-bottom: 17pt;
  padding-right: 17pt;
  padding-left: 17pt;
}
#location {
  padding: 0;
  margin: 0;
}
#location > img {
  height: 300px;
  width: 100%;
  padding: 0;
  margin: 0;
}
#footer * {
  margin: 0;
  background-color: black;
  color: white;
}
@[])
}
